---
layout: course
---
<h1>Python, Gráf, Programozás (2 órás)</h1>
<p class="audience">középiskola bármelyik osztály</p>
<p class="goal">Gráf alapú adattárolás és programozási alapismeretek</br>
Az szoftverfejlesztés során fontos készség az elvont adatstruktúrák megértése, tervezése és kezelése. Az egyik ilyen alapvető adatstruktúra a gráf, melyet számtalan rendszer használ információ rövid vagy hosszútávú tárolására és az entitások közötti kapcsolatok jelölésére. 
A szakkör során a tanulók megismerkedhetnek a gráf alapú adatreprezentáció alapjaival. Az elméleti ismeretket gyakorlati példák során szilárdítjuk meg, melyhez az ismert és ipari környezetben is gyakran használt Neo4J gráf alapú adatbázis rendszert használjuk. A rendszer látványos és könnyen érthető grafikus felületén keresztül térképezhetjük fel az adatbázisok tartalmát és szerkezetét. A szakkör célja, hogy a fiatalok a saját világukból és életükből vett problémák megoldásán keresztül ismerjék meg a fenn említett technológiákat és eszközöket.</p>

<h2>Mit csinál egy informatikus (szoftverfejlesztő)?</h2>
<p class="overview"><span class="emph">(15 perc)</span><br/>A tanulók saját tapasztalatai alpján felsoroljuk és megismerjük az informatika szakterületeit.</p>
<ul>
  <li>folyamat tervezés</li>
  <li>adatszerkezet tervezés</li>
  <li>fejlesztés (kódolás)</li>
  <li>tesztelés</li>
  <li>hibajavítás</li>
</ul>

<h2>Mi az a gráf?</h2>
<p class="overview"><span class="emph">(30 perc)</span><br/>Gráfelméleti alapfogalmak bevezetése.</p>
<ul>
  <li>gráf definíció: pontok és élek</li>
  <li>kapcsolatok leírása gráfként</li>
  <li>irányítás</li>
  <li>él-súlyok</li>
  <li>csomópont-tulajdonságok</li>
  <li>él-tulajdonságok</li>
</ul>

<h2>Szünet</h2>
<p class="overview"><span class="emph">(10 perc)</span>
  
<h2>Bevezetés a gráf adatbázisok világába</h2>
<p class="overview"><span class="emph">(20 perc)</span><br/>Neo4J gráf adatbázis szerkezete és a <a href="https://neo4j.com/developer/cypher-query-language/">Cypher lekérdező nyelv</a>. Üres adatbázis létrehozása és feltöltése egy <a href="https://neo4j.com/sandbox-v2/">példa adatbázison</a> keresztül.</p>
<ul>
  <li>Neo4J gráf modell</li>
  <li>Cypher szintaxis
    <ul>
      <li><details><summary>Példa</summary><pre class="code">
MATCH (m:Movie)<-[:RATED]-(u:User) <span class="comment">//Search for an existing graph pattern</span>
WHERE m.title CONTAINS "Matrix" <span class="comment">//Filter matching paths to only those matching a predicate</span>
WITH m.title AS movie, COUNT(*) AS reviews <span class="comment">//Count number of paths matched for each movie</span>
RETURN movie, reviews <span class="comment">//Specify columns to be returned by the statement</span>
ORDER BY reviews DESC <span class="comment">//Order by number of reviews, in descending order</span>
LIMIT 5; <span class="comment">//Only return first five records</span>
        </pre></details>
      </li>
    </ul>
  </li>
  <li>minta illesztés</li>
  <li>változók</li>
  <li>feltételek</li>
  <li>rendezés</li>
  <li>limitek</li>
  <li>utasítások: paraméterek és visszatérési érték</li>
  <li>összegző függvények</li>
</ul>

<h2>Gráfadatbázisok a gyakrolatban</h2>
<p class="overview"><span class="emph">(40 perc)</span><br/>Egy filmajánló rendszer megvalósítása IMDB alapján, egy <a href="https://neo4j.com/sandbox-v2/">példa adatbázison</a> keresztül.<br/>
Online angol nyelvű oktató anyag elérhető a <a href="https://guides.neo4j.com/sandbox/recommendations/index.html">Neo4J Sandboxban</a>
</p>

<h3>Keressünk egy példa filmet</h3>

<pre class="code">
match (m:Movie) return m.title
</pre>

<h3>Érdekes filmek lekérdezése felhasználónkként</h3>

<p>Ki szeretti még a Crimson Tide-ot?</p>

<pre class="code">
match (liked:Movie {title: "Crimson Tide"})<-[:RATED]-(user:User)
return *
</pre>

<p>Milyen más filmeket szeret aki szereti a Crimson Tide-ot?</p>

<pre class="code">
match (liked:Movie {title: "Crimson Tide"})<-[:RATED]-(user:User)
with liked, user limit 5
match (user)-[:RATED]->(other:Movie)
return * limit 35
</pre>

<p>Melyik filmet nézzem ha szeretem a Crimson Tide-ot?</p>

<pre class="code">
match (liked:Movie {title: "Crimson Tide"})<-[:RATED]-(user:User)-[:RATED]->(other:Movie)
return other.title as recommendation, collect(user.name) as usersWhoAlsoWatched, liked.title
limit 10
</pre>

<pre class="code">
match (liked:Movie {title: "Crimson Tide"})<-[:RATED]-(user:User)-[:RATED]->(other:Movie)
return other.title as recommendation, count(user) as similarity, liked.title as liked
order by similarity desc limit 10
</pre>

<h3>Hasonló műfajú filmek keresése</h3>

<p>halmazelméleti alapok</p>

<p><a href="https://en.wikipedia.org/wiki/Jaccard_index">Jaccard-féle hasonlóság</a> fogalma és kiszámítása</p>

<p>Milyen műfajokba sorolták be az Inception-t?</p>

<pre class="code">
match (liked:Movie {title: "Inception"})-[:IN_GENRE]->(genre:Genre)
return liked, genre limit 20
</pre>

<p>Közös műfajok megkeresése</p>

<pre class="code">
match (liked:Movie {title: "Inception"})-[:IN_GENRE]->(genre:Genre)<-[:IN_GENRE]-(other:Movie)
return liked.title, other.title, collect(genre.name) as intersection limit 20
</pre>

<p>Melyek az Inception műfajai</p>

<pre class="code">
match (liked:Movie {title: "Inception"})-[:IN_GENRE]->(genre:Genre)<-[:IN_GENRE]-(other:Movie)
with liked, other, collect(genre.name) as intersection
match (liked)-[:IN_GENRE]->(likedGenre:Genre)
return liked.title, other.title, collect(likedGenre.name) as likedGenres, intersection
</pre>

<p>A műfajok uniója</p>

<pre class="code">
match (liked:Movie {title: "Inception"})-[:IN_GENRE]->(genre:Genre)<-[:IN_GENRE]-(other:Movie)
with liked, other, collect(genre.name) as intersection
match (liked)-[:IN_GENRE]->(likedGenre:Genre)
with liked, other, collect(likedGenre.name) as likedGenres, intersection
match (other)-[:IN_GENRE]->(otherGenre:Genre)
with liked.title as liked, other.title as other, likedGenres, intersection, collect(otherGenre.name) as otherGenres
return liked, other, likedGenres, otherGenres, intersection, likedGenres+filter(genre in otherGenres where not genre in likedGenres) as both
</pre>

<p>Jaccard kiszámítása</p>

<pre class="code">
match (liked:Movie {title: "Inception"})-[:IN_GENRE]->(genre:Genre)<-[:IN_GENRE]-(other:Movie)
with liked, other, collect(genre.name) as intersection
match (liked)-[:IN_GENRE]->(likedGenre:Genre)
with liked, other, collect(likedGenre.name) as likedGenres, intersection
match (other)-[:IN_GENRE]->(otherGenre:Genre)
with liked.title as liked, other.title as other, likedGenres, intersection, collect(otherGenre.name) as otherGenres
with liked, other, likedGenres, otherGenres, intersection, likedGenres+filter(genre in otherGenres where not genre in likedGenres) as both
return liked, other, intersection, both, (size(intersection)*1.0)/(size(both)*1.0) as jaccard limit 10
</pre>

<p>Melyik hasonló műfajú filmet nézzem meg?</p>

<pre class="code">
match (liked:Movie {title: "Inception"})-[:IN_GENRE]->(genre:Genre)<-[:IN_GENRE]-(other:Movie)
with liked, other, collect(genre.name) as intersection
match (liked)-[:IN_GENRE]->(likedGenre:Genre)
with liked, other, collect(likedGenre.name) as likedGenres, intersection
match (other)-[:IN_GENRE]->(otherGenre:Genre)
with liked.title as liked, other.title as other, likedGenres, intersection, collect(otherGenre.name) as otherGenres
with liked, other, likedGenres, otherGenres, intersection, likedGenres+filter(genre in otherGenres where not genre in likedGenres) as both
return liked, other, intersection, both, (size(intersection)*1.0)/(size(both)*1.0) as jaccard
order by jaccard desc limit 10
</pre>